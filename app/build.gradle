/*
 * Copyright (c) 2019 Martin Pfeffer
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

apply plugin: 'com.android.application'

buildscript {
    repositories {
        google()
        jcenter()
        maven { url "https://jitpack.io" }
        maven {
            url 'https://maven.google.com'
        }
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:3.5.0-alpha12'
    }
}

repositories {
    jcenter()
    flatDir { dirs 'libs' }
    maven { url "https://jitpack.io" }
}

Properties props = new Properties()
InputStream is = new FileInputStream("$project.rootDir/build.properties")
props.load(is)
is.close()

task printProps {
    doFirst {
        println props.getProperty("app.version")
    }
}

def version_code = 9
def version_name = formatVersionCode(version_code)

task copyApk(type: Copy) {
    from '/Users/martin/AndroidStudioProjects/AndroidToolbox/app/build/outputs/apk/dev/debug'
    into '/Users/martin/Google Drive/public'
    exclude '*.json'
}

static def getDate() {
    def date = new Date()
    def dayOnly = true
    def formattedDate = date.format(dayOnly ? '_yyyy-MM-dd' : '_yyyy-MM-dd-hh-mm')
    return formattedDate
}

static def formatVersionCode(int versionCode) {
    String res = String.valueOf(versionCode)
    switch (res.length()) {
        case 1:
            return "0.0." + res
        case 2:
            return ("0." + res.charAt(0) + ".") + res.charAt(1)
        case 3:
            return "" + res.charAt(0) + ((".") + res.charAt(1)) + (".") + res.charAt(2)
    }
    return "-1"
}

android {
    compileSdkVersion 28
    defaultConfig {
        applicationId "io.celox.android_toolbox"
        minSdkVersion 24
        targetSdkVersion 28
        versionCode version_code
        versionName version_name
        multiDexEnabled true
        setProperty("archivesBaseName", "android-toolbox-$versionName")
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        debug {
            minifyEnabled false
            buildConfigField "long", "APP_CREATED", "1500000000000" + "L"
            versionNameSuffix '-d'
        }
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            buildConfigField "long", "APP_CREATED", System.currentTimeMillis() + "L"
            versionNameSuffix '-r'
        }
    }
    flavorDimensions 'default'
    productFlavors {
        pro {
            dimension = 'default'
            version_name += '-pro'
        }
        dev {
            dimension = 'default'
            applicationId "io.celox.android_toolbox.dev"
            versionName += '-dev'
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'com.google.android.material:material:1.0.0'
    implementation 'androidx.appcompat:appcompat:1.0.2'
    implementation 'androidx.recyclerview:recyclerview:1.0.0'
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    implementation 'org.jetbrains:annotations-java5:15.0'
    implementation 'androidx.preference:preference:1.1.0-alpha04'
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test.ext:junit:1.1.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.1.1'
    // utils
    implementation 'com.github.pepperonas:andbasx:0.3.0'
    implementation 'com.github.pepperonas:materialdialog:0.3.4'
    implementation 'com.github.pepperonas:aespreferences:0.0.6'
    // icons
    implementation 'com.mikepenz:iconics-core:3.0.2@aar'
    implementation 'com.mikepenz:google-material-typeface:3.0.1.0.original@aar'
    implementation 'com.mikepenz:material-design-iconic-typeface:2.2.0.2@aar'
    implementation 'com.mikepenz:community-material-typeface:2.0.46.1@aar'
    // graph-view
    implementation 'com.jjoe64:graphview:4.2.1'
    // preference fragment
    implementation 'com.github.machinarius:preferencefragment:0.1.1'
}
